Version="Ver 1.5.3-20210402"
'V1.5 2021/04/02 背景画像、透明度
DebugMode="false"
sbCr = Text.GetCharacter(13)
sbLf =  Text.GetCharacter(10)
sbCrLf = sbCr + sbLf
sbCrLf2 = sbCrLf + sbCrLf
sbDq = Text.GetCharacter(34)
MAXFILE = 2
TEXT_Y_AXIS=50
TEXT_X_AXIS=0

'init
PubInitLoad()
PubInitCommon()
Controls.ButtonClicked = EventBUTTONCLICK
GraphicsWindow.MouseUp = EventMouseDown
Timer.Tick = EventUPDATEINTERVAL
Timer.Interval = 5000

Sub PubInitLoad
  DataCurrent=1
  ConfFile1 = File.GetSettingsFilePath() + "-conf"
  For i=1 To MAXFILE
    DataFile[i] = File.GetSettingsFilePath() + "-" + i
  EndFor
  If DebugMode="true" Then
    userName= "TmpClip"
  Else
    userName= File.ReadLine(ConfFile1,1)
    confWidth = File.ReadLine(ConfFile1,2)
    confHeight =File.ReadLine(ConfFile1,3)
    confFontName = File.ReadLine(ConfFile1,4)
    confFontSize = File.ReadLine(ConfFile1,5)
    confBrushColor = File.ReadLine(ConfFile1,6)
    confBackgroundColor= File.ReadLine(ConfFile1,7)
    confDark= File.ReadLine(ConfFile1,8)
    confOpacity = File.ReadLine(ConfFile1,9)
    confUrls[1] = File.ReadLine(ConfFile1,10)
    If Text.GetIndexOf(confUrls[1],":") = 0 Then
      confUrls[1]=Program.Directory +"\" + confUrls[1]
    EndIf
    confUrls[2] = File.ReadLine(ConfFile1,11)
    If Text.GetIndexOf(confUrls[2],":") = 0 Then
      confUrls[2]=Program.Directory +"\" + confUrls[2]
    EndIf
    For i=1 To MAXFILE
      TextData[i]=File.ReadContents(DataFile[i])
      TextUnsaved[i] = 0
    EndFor
  EndIf
  SaveFileName = "TmpClip☆" + username
  tstart=Clock.ElapsedMilliseconds
EndSub

Sub PubInitCommon
  ExitButton = Controls.AddButton("終了", 0, -10)
  Controls.SetSize(ExitButton, 40, 30)
  SaveButton1 = Controls.AddButton(title[1], 100, 1)
  Controls.SetSize(SaveButton1, 400, 30)
  GraphicsWindow.Title =  Version + " 開始時刻： " + Clock.Time  + " " + SaveFileName
  GraphicsWindow.Width = confWidth
  GraphicsWindow.Height = confHeight
  GraphicsWindow.BrushColor = confBrushColor
  GraphicsWindow.BackgroundColor = confBackgroundColor
  
  GraphicsWindow.FontName = confFontName
  GraphicsWindow.FontSize = confFontSize
  
  memo = Controls.AddMultiLineTextBox(TEXT_X_AXIS, TEXT_Y_AXIS)
  Controls.SetSize(memo, GraphicsWindow.Width-TEXT_X_AXIS, GraphicsWindow.Height-TEXT_Y_AXIS)
  Controls.SetTextBoxText(memo, TextData[DataCurrent])
  SubNoSaveAndInit()
  aWall[1]=ImageList.LoadImage(confUrls[1])
  aWall[2]=ImageList.LoadImage(confUrls[2])
  GraphicsWindow.DrawImage(aWall[1],0,0)
  GraphicsWindow.DrawResizedImage(aWall[2],TEXT_X_AXIS,TEXT_Y_AXIS,GraphicsWindow.Width-TEXT_X_AXIS, GraphicsWindow.Height-TEXT_Y_AXIS)
  
  ExtLib.DisableMaxMinExitButton()
  If Text.ConvertToUpperCase(confDark) = "DARK" Then
    a=ExtLib.SetDarkmodeControl(memo,confOpacity)
    ExtLib.SetDarkMode_Explorer("dark")
  EndIf
  ExtLib.SetFocusControl(memo)
EndSub

Sub SubNoSaveAndInit
  SubUpdateButtonTitle()
  SubCopyLength()
  sense = -1
EndSub

Sub SubCopyLength
  LengthBefore=LengthAfter
EndSub

Sub SubSaveAndInit
  SubUpdateButtonTitle()
  SubCopyLength()
  OneSaveLocalFile()
  sense = -1
EndSub

Sub EventBUTTONCLICK
  SubSaveAndInit()
  titleStr = "一時保存しました"
  SubUpdateTitleBarText()
  If Controls.LastClickedButton = SaveButton1 Then
    OneSwitchMemo()
  ElseIf Controls.LastClickedButton = ExitButton Then
    Program.End()
  EndIf
EndSub

Sub EventMOUSEDOWN
  If GraphicsWindow.MouseY < TEXT_Y_AXIS then
    SubSaveAndInit()
    titleStr = "一時保存しました"
    SubUpdateTitleBarText()
    OneSwitchMemo()
  EndIf
EndSub

Sub EventUPDATEINTERVAL
  OneTextChangedChk()
  If sense = TextUnsaved[DataCurrent] And TextUnsaved[DataCurrent] <> 0Then
    SubSaveAndInit()
    titleStr="一時保存しました"
    SubUpdateTitleBarText()
  Else
    If  TextUnsaved[DataCurrent] <> 0 Then
      'titleStr="* 執筆の経過時間   約" + sense + ":" +TextUnsaved[DataCurrent]+ "LengthAfter:"+ LengthAfter+"LengthBefore:"+ LengthBefore
      titleStr="* 執筆の経過時間   約"
    Else
      titleStr="執筆の経過時間   約"
    EndIf
    SubUpdateTitleBarText()
    sense = TextUnsaved[DataCurrent]
  EndIf
EndSub

Sub OneSwitchMemo
  If DataCurrent = 1 Then
    Controls.SetButtonCaption(SaveButton1,title[2])
    Controls.Move(SaveButton1,"400","1")
    GraphicsWindow.BackgroundColor = confBrushColor
    DataCurrent=2
  Else
    Controls.SetButtonCaption(SaveButton1,title[1])
    Controls.Move(SaveButton1,"100","1")
    GraphicsWindow.BackgroundColor = confBackgroundColor
    DataCurrent=1
  EndIf
  Controls.SetTextBoxText(memo, TextData[DataCurrent])
  ExtLib.SetFocusControl(memo)
  SubNoSaveAndInit()
EndSub

Sub OneTextChangedChk
  SubCopyFromTextBox()
  If LengthAfter <> LengthBefore Then
    TextUnsaved[DataCurrent] = 1 + TextUnsaved[DataCurrent]
    SubCopyLength()
  EndIf
EndSub

Sub SubUpdateTitlebarText
  tmiddle= Clock.ElapsedMilliseconds
  GraphicsWindow.Title = SaveFileName + " " + titleStr + Math.Ceiling((tmiddle-tstart)/1000/60) + "分"   + title[i] + LengthAfter + "文字" + "                     現在時刻： " + Clock.Time
EndSub

Sub SubUpdateButtonTitle
  SubCopyFromTextBox()
  For i=1 To MAXFILE
    title[i]=Text.GetSubText(TextData[i],1,Text.GetIndexOf(TextData[i],sbCrLf))
  EndFor
  Controls.SetButtonCaption(SaveButton1,title[DataCurrent])
EndSub

Sub SubCopyFromTextBox
  TextData[DataCurrent]=Controls.GetTextBoxText(memo)
  LengthAfter = Text.GetLength(TextData[DataCurrent])
EndSub

Sub OneSaveLocalFile
  tmpFileName = "-" + Clock.Year +"／"+ Clock.Month +"／"+ Clock.Day  +"-"+  Clock.Hour + Clock.Minute + Clock.Second + ".tmp"
  For i=1 To MAXFILE
    If  Text.GetLength(TextData[i]) > 0 and  TextUnsaved[i] <> 0 Then
      File.WriteContents(DataFile[i] + tmpFileName, TextData[i])
      File.WriteContents(DataFile[i], TextData[i])
      TextUnsaved[i] = 0
      Extlib.DeleteWithRecycle(DataFile[i] + tmpFileName)
    EndIf
  EndFor
EndSub
