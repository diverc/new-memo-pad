Version="Ver 1.3.0-20210331"
'和暦対応
'V1.3 2021/03/31 Mutex
'V1.2 2021/03/25 ごみバコ実装
'V1.1 2021/03/24 DarkMode実装実装
'V1.0 2021/03/20 簡易メモ帳として実装
'V0.11 2019/05/27 バックアップをわかるようにした
'V0.11 2019/05/13 バックアップ機能
'V0.10 2019/04/25 ほかの人用のカスタマイズ版

'Interbalではなく文字を入力するたびに
'メモリにコピーする。Intervalでコントロールに触れない

'DebugMode="true"
DebugMode="false"
sbCr = Text.GetCharacter(13)
sbLf =  Text.GetCharacter(10)
sbCrLf = sbCr + sbLf
sbCrLf2 = sbCrLf + sbCrLf
sbDq = Text.GetCharacter(34)
MAXFILE = 2

'init
InitLoad()
InitCommon()
Controls.TextTyped = TextChanged
Controls.ButtonClicked = BUTTONCLICK
Timer.Interval = 5000
Timer.Tick = UPDATEINTERVAL

Sub InitLoad
  DataCurrent=1
  ConfFile1 = File.GetSettingsFilePath() + "-conf"
  For i=1 To MAXFILE
    DataFile[i] = File.GetSettingsFilePath() + "-" + i
  endfor
  If DebugMode="true" Then
    userName= "memo" 
    inittext = "memo" 
    confWidth = 800
    confHeight = 600
    confFontName = "Meiryo UI"
    confFontSize = 16
    confBrushColor = "Black"
    confBackgroundColor = "green"
    File.WriteLine(ConfFile1,1,userName)
    File.WriteLine(ConfFile1,2,confWidth)
    File.WriteLine(ConfFile1,3,confHeight)
    File.WriteLine(ConfFile1,4,confFontName)
    File.WriteLine(ConfFile1,5,confFontSize)
    File.WriteLine(ConfFile1,6,confBrushColor)
    File.WriteLine(ConfFile1,7,confBackgroundColor)
  Else
    userName= File.ReadLine(ConfFile1,1)
    confWidth = File.ReadLine(ConfFile1,2)
    confHeight =File.ReadLine(ConfFile1,3)
    confFontName = File.ReadLine(ConfFile1,4)
    confFontSize = File.ReadLine(ConfFile1,5)
    confBrushColor = File.ReadLine(ConfFile1,6)
    confBackgroundColor= File.ReadLine(ConfFile1,7)
    confDark= File.ReadLine(ConfFile1,8)
    For i=1 To MAXFILE
      TextData[i]=File.ReadContents(DataFile[i]) 
      TextUnsaved[i] = 0
    endfor
  EndIf
  SaveFileName = "Edit" + username
  tstart=Clock.ElapsedMilliseconds
Endsub

Sub InitCommon
  ExitButton = Controls.AddButton("終了", 0, -10)
  Controls.SetSize(ExitButton, 40, 30)
  SaveButton1 = Controls.AddButton(title[1], 100, 1)
  Controls.SetSize(SaveButton1, 400, 30)
  GraphicsWindow.Title =  Version + " 開始時刻： " + Clock.Time  + " " + SaveFileName
  GraphicsWindow.Width = confWidth
  GraphicsWindow.Height = confHeight
  GraphicsWindow.BrushColor = confBrushColor
  GraphicsWindow.BackgroundColor = confBackgroundColor

  GraphicsWindow.FontName = confFontName
  GraphicsWindow.FontSize = confFontSize
  
  memo = Controls.AddMultiLineTextBox(1, 50)
  Controls.SetSize(memo, GraphicsWindow.Width-2, GraphicsWindow.Height-55)
  Controls.SetTextBoxText(memo, TextData[DataCurrent])
  
  ScanTitle()
  'ExtLib.DisableExitButton()
  ExtLib.DisableMaxMinExitButton()
  if Text.ConvertToUpperCase(confDark) = "DARK" then
    a=ExtLib.SetDarkmodeControl(memo)
    ExtLib.SetDarkMode_Explorer("dark")
  EndIf
  ExtLib.SetFocusControl(memo)
  'ExtLib.WSendKeys("{{kanji}}")
  'ExtLib.SendIMEKeys()
  'ExtLib.SendIMEKey2hanzen()
EndSub

Sub textChanged
  ExtLib.MutexWaitOne()
  TextUnsaved[DataCurrent] = 1 + TextUnsaved[DataCurrent]
  GraphicsWindow.Title = "changed : " + TextUnsaved[DataCurrent]
  ExtLib.MutexRelease()
EndSub

Sub UPDATEINTERVAL
  ExtLib.MutexWaitOne()
  if sense = TextUnsaved[DataCurrent] And TextUnsaved[DataCurrent] <> 0Then
    ScanTitle()
    SavelocalFile()
    TextUnsaved[DataCurrent]=0
    sense = -1
    GraphicsWindow.Title = "一時保存しました" + Math.Ceiling((tmiddle-tstart)/1000/60) + "分"  + title[i] + len + "文字"
  Else
    tmiddle= Clock.ElapsedMilliseconds
    GraphicsWindow.Title = "執筆の経過時間   約" + Math.Ceiling((tmiddle-tstart)/1000/60) + "分"  + SaveFileName + "  " +len + "文字"
    sense = TextUnsaved[DataCurrent]
  EndIf
  ExtLib.MutexRelease()
EndSub


Sub BUTTONCLICK
  tmiddle= Clock.ElapsedMilliseconds
  'TextUnsaved[DataCurrent] = TextUnsaved[DataCurrent] + 1
  ScanTitle()
  SaveLocalFile()
  If Controls.LastClickedButton = SaveButton1 Then
    if DataCurrent = 1 then
      Controls.SetButtonCaption(SaveButton1,title[2])
      Controls.Move(SaveButton1,"400","1")
      GraphicsWindow.BackgroundColor = confBrushColor
      DataCurrent=2
    else
      Controls.SetButtonCaption(SaveButton1,title[1])
      Controls.Move(SaveButton1,"100","1")
      GraphicsWindow.BackgroundColor = confBackgroundColor
      DataCurrent=1
    endif
    Controls.SetTextBoxText(memo, TextData[DataCurrent])
    sense = -100
  elseIf Controls.LastClickedButton = ExitButton Then
    Program.End()
  EndIf
  GraphicsWindow.Title = "一時保存しました" + Math.Ceiling((tmiddle-tstart)/1000/60) + "分"  + " file: " + title[i]
  ExtLib.SetFocusControl(memo)
EndSub

Sub ScanTitle
  len = Text.GetLength(Controls.GetTextBoxText(memo) )
  TextData[DataCurrent]=Controls.GetTextBoxText(memo)
  For i=1 To MAXFILE
    title[i]=Text.GetSubText(TextData[i],1,Text.GetIndexOf(TextData[i],sbCrLf))
  EndFor
  Controls.SetButtonCaption(SaveButton1,title[DataCurrent])
EndSub

Sub SaveLocalFile
  tmpFileName = "-" + Clock.Year +"／"+ Clock.Month +"／"+ Clock.Day  +"-"+  Clock.Hour + Clock.Minute + Clock.Second + ".tmp"
  For i=1 To MAXFILE
    if  Text.GetLength(TextData[i]) > 0 and  TextUnsaved[i] <> 0 then
      File.WriteContents(DataFile[i] + tmpFileName, TextData[i])
      File.WriteContents(DataFile[i], TextData[i])
      TextUnsaved[i] = 0
      Extlib.DeleteWithRecycle(DataFile[i] + tmpFileName)
    endif
  EndFor
EndSub
